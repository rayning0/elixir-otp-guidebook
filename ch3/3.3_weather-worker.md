## Weather App Worker

Make [**free account on OpenWeather**](https://openweathermap.org/appid) to get an [API key](https://home.openweathermap.org/api_keys). In my `.bash_profile`, I added line `export WEATHER_API_KEY=...` for this new environment variable.

API call to [OpenWeather](https://openweathermap.org): `http://api.openweathermap.org/data/2.5/weather?q=#{location}&units=metric&appid=#{API key}`

This worker must get temperature of given location from OpenWeather and parse its results. Code: [lib/worker.ex](https://github.com/rayning0/metex/blob/master/lib/worker.ex):
```
defmodule Metex.Worker do
  def temperature_of(location) do
    result = url_for(location) |> HTTPoison.get() |> parse_response

    case result do
      {:ok, temp} ->
        "#{location}: #{temp}Â°C"

      :error ->
        "#{location} not found"
    end
  end

  # "units=metric" returns Celsius. Without it, this API call returns Kelvin.
  def url_for(location) do
    location = URI.encode(location)
    "http://api.openweathermap.org/data/2.5/weather?q=#{location}&units=metric&appid=#{apikey()}"
  end

  # Uses pattern matching to get "body". If API result does not match this map exactly, view as error
  defp parse_response({:ok, %HTTPoison.Response{body: body, status_code: 200}}) do
    body |> Jason.decode!() |> compute_temperature
  end

  # Any other HTTPoison response that's NOT 200 status is an error
  defp parse_response(_) do
    :error
  end

  defp compute_temperature(json) do
    try do
      # Rounds temp to nearest 1 decimal point
      temp = json["main"]["temp"] |> Float.round(1)
      {:ok, temp}
    rescue
      # Any failure in parsing JSON response is error
      _ -> :error
    end
  end

  defp apikey do
    System.get_env("WEATHER_API_KEY")
  end
end
```
[defp/2](https://hexdocs.pm/elixir/Kernel.html#defp/2) functions are `private functions`, only accessible from inside the module in which they are defined. Trying to access a private function from outside its defined module gives an `UndefinedFunctionError` exception.

Let's break down each part of this code in `iex`:
```
> "Los Angeles, California" |> Metex.Worker.url_for |> HTTPoison.get

{:ok,
 %HTTPoison.Response{
   status_code: 200,
   body: "{\"coord\":{\"lon\":-118.2437,\"lat\":34.0522},\"weather\":[{\"id\":802,\"main\":\"Clouds\",\"description\":\"scattered clouds\",\"icon\":\"03d\"}],\"base\":\"stations\",\"main\":{\"temp\":15.11,\"feels_like\":14.22,\"temp_min\":12.47,\"temp_max\":17.77,\"pressure\":1028,\"humidity\":59},\"visibility\":10000,\"wind\":{\"speed\":2.57,\"deg\":0},\"clouds\":{\"all\":40},\"dt\":1704217843,\"sys\":{\"type\":2,\"id\":2075946,\"country\":\"US\",\"sunrise\":1704207518,\"sunset\":1704243285},\"timezone\":-28800,\"id\":5368361,\"name\":\"Los Angeles\",\"cod\":200}",
   headers: [
     {"Server", "openresty"},
     {"Date", "Tue, 02 Jan 2024 17:52:23 GMT"},
     {"Content-Type", "application/json; charset=utf-8"},
     {"Content-Length", "482"},
     {"Connection", "keep-alive"},
     {"X-Cache-Key",
      "/data/2.5/weather?q=los%20angeles,%20california&units=metric"},
     {"Access-Control-Allow-Origin", "*"},
     {"Access-Control-Allow-Credentials", "true"},
     {"Access-Control-Allow-Methods", "GET, POST"}
   ],
...
 }}
```
Use pattern matching to get `body`:
- This is a 2-element tuple.
- First element of tuple is an `:ok` atom, followed by a structure representing the response.
- The response is type `HTTPoison.Response` and contains at least 2 fields.
- `status_code` is 200, a successful HTTP GET request.
- The value of `body` is captured in `body`.
```
 > {:ok, %HTTPoison.Response{body: body, status_code: 200}} = "Los Angeles, California" |> Metex.Worker.url_for |> HTTPoison.get
 > body

 "{\"coord\":{\"lon\":-118.2437,\"lat\":34.0522},\"weather\":[{\"id\":800,\"main\":\"Clear\",\"description\":\"clear sky\",\"icon\":\"01d\"}],\"base\":\"stations\",\"main\":{\"temp\":15.11,\"feels_like\":14.65,\"temp_min\":12.99,\"temp_max\":17.95,\"pressure\":1025,\"humidity\":59},\"visibility\":10000,\"wind\":{\"speed\":3.09,\"deg\":100},\"clouds\":{\"all\":0},\"dt\":1704218445,\"sys\":{\"type\":1,\"id\":3694,\"country\":\"US\",\"sunrise\":1704207518,\"sunset\":1704243285},\"timezone\":-28800,\"id\":5368361,\"name\":\"Los Angeles\",\"cod\":200}"
```
This API call could instead return an error:
- This is a 2-element tuple.
- First element of the tuple is an `:error` atom, followed by a structure that represents the error.
- The response is type `HTTPoison.Error` and contains at least one field, `reason`.
- The reason for the error is captured in `reason`.
```
{:error, %HTTPoison.Error{reason: reason}}
```

Use [Jason.decode!/2](https://hexdocs.pm/jason/Jason.html#decode!/2) to change successful JSON result into [map](https://hexdocs.pm/elixir/Map.html):
```
> weather_map = body |> Jason.decode!

%{
  "base" => "stations",
  "clouds" => %{"all" => 0},
  "cod" => 200,
  "coord" => %{"lat" => 34.0522, "lon" => -118.2437},
  "dt" => 1704218445,
  "id" => 5368361,
  "main" => %{
    "feels_like" => 14.65,
    "humidity" => 59,
    "pressure" => 1025,
    "temp" => 15.11,
    "temp_max" => 17.95,
    "temp_min" => 12.99
  },
  "name" => "Los Angeles",
  "sys" => %{
    "country" => "US",
    "id" => 3694,
    "sunrise" => 1704207518,
    "sunset" => 1704243285,
    "type" => 1
  },
  "timezone" => -28800,
  "visibility" => 10000,
  "weather" => [
    %{
      "description" => "clear sky",
      "icon" => "01d",
      "id" => 800,
      "main" => "Clear"
    }
  ],
  "wind" => %{"deg" => 100, "speed" => 3.09}
}
```
Grab "temp" (Celsius) from `weather_map` and round it to nearest 1 decimal point:
```
> weather_map["main"]["temp"] |> Float.round(1)

15.1
```
